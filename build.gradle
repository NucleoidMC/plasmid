plugins {
  id 'fabric-loom' version '0.5-SNAPSHOT'
  id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

def env = System.getenv()

def build_number = env.BUILDNUM_FOR_VERSION

// check if we're in actions environment
if (build_number) {
  version = "$version.$build_number"
} else {
  version = "$version+local"
}

minecraft {
  accessWidener = file("src/main/resources/plasmid.accesswidener")
}

repositories {
  maven { url = "https://jitpack.io/" }
  maven { url = "https://maven.nucleoid.xyz/" }
}

dependencies {
  minecraft "com.mojang:minecraft:${project.minecraft_version}"
  mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

  modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

  modRuntime("com.github.SuperCoder7979:databreaker:0.2.6") {
    exclude module: "fabric-loader"
  }

  modImplementation 'fr.catcore:server-translations-api:1.4.0'
  include 'fr.catcore:server-translations-api:1.4.0'

  modImplementation 'xyz.nucleoid:packet-tweaker:0.1.1'
  include 'xyz.nucleoid:packet-tweaker:0.1.1'

  modImplementation 'xyz.nucleoid:fantasy:0.2.4'
  include 'xyz.nucleoid:fantasy:0.2.4'

  modImplementation 'xyz.nucleoid:leukocyte:0.1.3'
}

processResources {
  inputs.property "version", project.version

  from(sourceSets.main.resources.srcDirs) {
    include "fabric.mod.json"
    expand "version": project.version
  }

  from(sourceSets.main.resources.srcDirs) {
    exclude "fabric.mod.json"
  }
}

tasks.withType(JavaCompile) {
  options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = "sources"
  from sourceSets.main.allSource
}

jar {
  from "LICENSE"
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifact(remapJar) {
        builtBy remapJar
      }
      artifact(sourcesJar) {
        builtBy remapSourcesJar
      }
    }
  }

  repositories {
    if (env.MAVEN_URL) {
      maven {
        credentials {
          username env.MAVEN_USERNAME
          password env.MAVEN_PASSWORD
        }
        url env.MAVEN_URL
      }
    } else {
      mavenLocal()
    }
  }
}
