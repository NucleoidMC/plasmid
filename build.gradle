plugins {
  id 'fabric-loom' version '1.1.+'
  id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
group = project.maven_group

sourceSets {
  testmod {
    runtimeClasspath += main.runtimeClasspath
    compileClasspath += main.compileClasspath
  }
}

loom {
  runs {
    testmodClient {
      client()
      ideConfigGenerated project.rootProject == project
      name = "Test Mod Client"
      source sourceSets.testmod
    }
    testmodServer {
      server()
      ideConfigGenerated project.rootProject == project
      name = "Test Mod Server"
      source sourceSets.testmod
    }
  }

  accessWidenerPath = file("src/main/resources/plasmid.accesswidener")
  runtimeOnlyLog4j = true
}

def env = System.getenv()

def minor = env.get("MINOR_VERSION")

version = "${project.mod_version}"
def versionGame = version;

def build = env.get("BUILD_NUMBER")


if (minor == null) {
  version += "-local"
  versionGame += "-local"
} else {
  version += ".${minor}"
  versionGame += ".${minor}"
  if (build != null) {
    version += "-SNAPSHOT"
    versionGame += "-build." + build
  }
}

versionGame += "+" + project.minecraft_version
version += "+" + project.minecraft_version

repositories {
  maven { url = "https://maven.nucleoid.xyz/" }
  maven { url = "https://maven.gegy.dev/" }
  maven { url = "https://api.modrinth.com/maven/" }
  maven { url 'https://jitpack.io' }
}


dependencies {
  minecraft "com.mojang:minecraft:${project.minecraft_version}"
  mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

  modApi "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

  modApi include('xyz.nucleoid:server-translations-api:2.0.0+1.20')
  modApi include('xyz.nucleoid:packet-tweaker:0.4.0+1.19.4')
  modApi include('xyz.nucleoid:fantasy:0.4.11+1.20-rc1')
  modApi include('xyz.nucleoid:more-codecs:0.3.2+1.20')
  modApi include('xyz.nucleoid:stimuli:0.4.6+1.20')
  modApi include('xyz.nucleoid:map-templates:0.1.8+1.20')
  modApi include('eu.pb4:polymer-core:0.5.6+1.20.1')
  modApi include('eu.pb4:polymer-resource-pack:0.5.6+1.20.1')
  modApi include('eu.pb4:polymer-blocks:0.5.6+1.20.1')
  modApi include('eu.pb4:polymer-virtual-entity:0.5.6+1.20.1')
  modApi include('eu.pb4:hologram-api:0.2.4+1.20')
  modApi include('eu.pb4:sgui:1.2.2+1.20')
  modApi include('eu.pb4:sidebar-api:0.1.2+1.19.3')
  modApi include("eu.pb4:placeholder-api:2.1.2+1.20.1")
  modApi include("eu.pb4:map-canvas-api:0.2.2+1.19.4")
  modApi include("eu.pb4:player-data-api:0.2.2+1.19.3")
  modApi include("eu.pb4:predicate-api:0.1.2+1.20")

  modCompileOnly('xyz.nucleoid:disguiselib-fabric:1.3.2')
  modCompileOnly('maven.modrinth:afkdisplay:1.1.0')
  modCompileOnly('eu.pb4:polymer-autohost:0.5.6+1.20.1')

  include(implementation(annotationProcessor("com.github.llamalad7.mixinextras:mixinextras-fabric:${project.mixin_extras_version}")))

  testmodImplementation sourceSets.main.output
}

processResources {
  inputs.property "version", versionGame

  filesMatching("fabric.mod.json") {
    expand "version": versionGame
  }
}

tasks.withType(JavaCompile).configureEach {
  it.options.encoding = "UTF-8"
  it.options.release = 17
}

java {
  withSourcesJar()
}

jar {
  from("LICENSE") {
    rename { "${it}_${project.archivesBaseName}"}
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifact(remapJar) {
        builtBy remapJar
      }
      artifact(sourcesJar) {
        builtBy remapSourcesJar
      }
    }
  }

  repositories {
    if (env.MAVEN_URL) {
      maven {
        credentials {
          username env.MAVEN_USERNAME
          password env.MAVEN_PASSWORD
        }
        url env.MAVEN_URL
      }
    } else {
      mavenLocal()
    }
  }
}
