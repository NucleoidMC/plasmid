plugins {
  id 'fabric-loom' version '1.6.+'
  id 'maven-publish'
  id "com.modrinth.minotaur" version "2.+"
}

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

archivesBaseName = project.archives_base_name
group = project.maven_group

sourceSets {
  testmod {
    runtimeClasspath += main.runtimeClasspath
    compileClasspath += main.compileClasspath
  }
}

loom {
  runs {
    testmodClient {
      client()
      ideConfigGenerated project.rootProject == project
      name = "Test Mod Client"
      source sourceSets.testmod
    }
    testmodServer {
      server()
      ideConfigGenerated project.rootProject == project
      name = "Test Mod Server"
      source sourceSets.testmod
    }
  }

  accessWidenerPath = file("src/main/resources/plasmid.accesswidener")
  runtimeOnlyLog4j = true
}

def env = System.getenv()

def minor = env.get("MINOR_VERSION")

version = "${project.mod_version}"
def versionGame = version;

def build = env.get("BUILD_NUMBER")

if (minor == null) {
  version += ".9999-local"
  versionGame += ".9999-local"
} else {
  version += ".${minor}"
  versionGame += ".${minor}"
  if (build != null) {
    version += "-SNAPSHOT"
    versionGame += "-build." + build
  }
}

versionGame += "+" + project.minecraft_version
version += "+" + project.minecraft_version

repositories {
  maven { url = "https://maven.nucleoid.xyz/" }
  maven { url = "https://maven.gegy.dev/" }
  maven { url = "https://api.modrinth.com/maven/" }
  maven { url 'https://jitpack.io' }
  maven {
    name = "TerraformersMC"
    url = "https://maven.terraformersmc.com/"
  }
  maven {
    name = "Ladysnake Libs"
    url = 'https://maven.ladysnake.org/releases'
  }
}


dependencies {
  minecraft "com.mojang:minecraft:${project.minecraft_version}"
  mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

  modApi "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

  modApi include('xyz.nucleoid:server-translations-api:2.3.0+1.20.5-rc2')
  modApi include('xyz.nucleoid:packet-tweaker:0.5.1+1.20.6')
  modApi include('xyz.nucleoid:fantasy:0.6.2+1.20.6')
  modApi include('xyz.nucleoid:more-codecs:0.3.3+1.20.2')
  modApi include('xyz.nucleoid:stimuli:0.4.11+1.20.6')
  modApi include('xyz.nucleoid:map-templates:0.1.9+1.20.4')
  modApi include('xyz.nucleoid:substrate:0.2.2+1.20.1')
  modApi 'eu.pb4:polymer-core:0.8.2+1.20.6'
  modApi 'eu.pb4:polymer-resource-pack:0.8.2+1.20.6'
  modApi 'eu.pb4:polymer-blocks:0.8.2+1.20.6'
  modApi 'eu.pb4:polymer-virtual-entity:0.8.2+1.20.6'
  modApi include('eu.pb4:sgui:1.5.1+1.20.5')
  modApi include('eu.pb4:sidebar-api:0.4.0+1.20.5')
  modApi include("eu.pb4:placeholder-api:2.4.0-pre.1+1.20.5")
  modApi include("eu.pb4:map-canvas-api:0.3.0+1.20.6")
  modApi include("eu.pb4:player-data-api:0.5.0+1.20.5")
  modApi include("eu.pb4:predicate-api:0.4.0+1.20.5")

  modImplementation include("me.lucko:fabric-permissions-api:0.2-SNAPSHOT")

  modCompileOnly('xyz.nucleoid:disguiselib-fabric:1.3.2')
  modCompileOnly('maven.modrinth:afkdisplay:1.1.0')
  modCompileOnly('eu.pb4:polymer-autohost:0.8.2+1.20.6')
  modCompileOnly("dev.emi:trinkets:3.7.1")

  testmodImplementation sourceSets.main.output
}

processResources {
  inputs.property "version", versionGame

  filesMatching("fabric.mod.json") {
    expand "version": versionGame
  }
}

tasks.withType(JavaCompile).configureEach {
  it.options.encoding = "UTF-8"
  it.options.release = 21
}

java {
  withSourcesJar()
}

jar {
  from("LICENSE") {
    rename { "${it}_${project.archivesBaseName}"}
  }
}

if (System.getenv("MODRINTH")) {
  modrinth {
    token = System.getenv("MODRINTH")
    projectId = 'xUvfRSZh'// The ID of your modrinth project, slugs will not work.
    versionNumber = "" + version // The version of the mod to upload.
    versionType = "release"
    uploadFile = remapJar // This links to a task that builds your mod jar and sets "uploadFile" to the mod jar.
    gameVersions = [((String) project.minecraft_version)]
    changelog = System.getenv("CHANGELOG")
    dependencies {
      required.project 'polymer'
      required.project 'fabric-api'
      optional.project 'afkdisplay'
      optional.project 'disguiselib'
      embedded.project 'placeholder-api'
    }
    loaders = ["fabric", "quilt"]
  }

  remapJar {
    finalizedBy project.tasks.modrinth
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }

  repositories {
    if (env.MAVEN_URL) {
      maven {
        credentials {
          username env.MAVEN_USERNAME
          password env.MAVEN_PASSWORD
        }
        url env.MAVEN_URL
      }
    } else {
      mavenLocal()
    }
  }
}
